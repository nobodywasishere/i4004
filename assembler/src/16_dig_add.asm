; 16-DIGIT DECIMAL ADDITION ROUTINE
; TAKEN FROM MCS-4 MANUAL

ADDITN
    FIM r0, 0   ; IR(0-1) = 0
    FIM p2, 48  ; IR(4)=3, IR(5)=0
    LDM 0       ; LOAD 0 TO AC
    XCH 6       ; EXCHANGE C(AC) and IR(6)
    CLC         ; CLEAR CARRY REG
ADI
    SRC p2      ; DEFINE RAM ADDRESS $<1
    RDM         ; READ RAM TO AC
    SRC p0      ; DEFINE RAM ADDRESS
    ADM         ; ADD C(RAM) TO AC, CARRY ENABLED
    DAA         ; DECIMAL ADDRESS ACC
    WRM         ; WRITE AC TO RAM
    INC 1       ; INCREMENT IR(1)
    INC 5       ; INCREMENT IR(5)
    ISZ 6, ADI  ; IR(6)=IR(6)+1, SKIP IF C(IR(6))=0

OVERFL
    JCN CN, XXX ; TEST CARRY, JUMP IF
    JUN NEXT    ; SEE NOTE $<2
XXX
    LDM 0       ; LOAD AC WITH 0
    XCH 10      ; EXCHANGE IR(10) AND AC
OVFL1
    FIM p1, 216 ; IR(1)=8, IR(2)=13 [x]
    JMS PRINT
    ISZ 10, OVFL1   ;IR(10)=IR(10)+1, SKIP IF IR(10)=0
    FIM p2, 0   ; SET IR(4-5)=0
    JMS CLRRAM  ; CLEAR RAM DATA
    JUN NEXT    ; SEE NOTE $<2

CN=5

; DUMMY ARGUMENTS
CLRRAM=0xC0
NEXT=0x80
PRINT=0xE8

; $<1 RAM ADDRESSING DEFINE AS TO STANDARDS IN
; SPEC SHEET.
; BITS NUMBERED FROM LEFT TO RIGHT MSB TO LSB
; 0 1 2 3 4 5 6 7
; BITS 0-1 SELECT RAM CHIP 1 OF 4
; BITS 2-3 SELECT RAM REGISTER 1 OF 4
; BITS 4-7 SELECT REGISTER CHARACTER 1 OF 16
;
; $<2 NEXT, PRINT, AND CLRRAM ARE ADDRESS TAGS USED FOR
; ASSEMBLY
; NEXT CAN BE THE RETURN POINT OF THIS ROUTINE
; CLRRAM AND PRINT ARE ROUTINES CALLED BY THIS PROGRAM
